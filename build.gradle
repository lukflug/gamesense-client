
buildscript {
	repositories {
		jcenter()
		maven {
			name = 'forge'
			url = 'https://files.minecraftforge.net/maven'
		}
		maven {
			name = 'SpongePowered'
			url = 'https://repo.spongepowered.org/repository/maven-public/'
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:${forgegradleVersion}"
		classpath "org.spongepowered:mixingradle:${mixingradleVersion}"
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version project.modVersion
group project.modGroup

configurations {
	embed
	compile.extendsFrom(embed)
}

compileJava {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

minecraft {
	mappings channel: 'stable', version: project.mcpVersion
	runs {
		client {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
			property 'forge.logging.console.level', 'debug'
			property 'fml.coreMods.load', 'com.gamesense.mixin.GameSenseMixinLoader'
		}
	}
	accessTransformer = file('src/main/resources/gamesense_at.cfg')
}

repositories {
	maven {
		name = 'spongepowered-repo'
		url = 'https://repo.spongepowered.org/repository/maven-public/'
	}
	maven {
		name = 'impactdevelopment-repo'
		url = 'https://impactdevelopment.github.io/maven/'
	}
	maven {
		name = 'swt-repo'
		url = "https://maven-eclipse.github.io/maven"
	}
	maven {
		name = 'jitpack.io'
		url = "https://jitpack.io"
	}
	maven {
		name = 'lukflug'
		url = 'https://lukflug.github.io/maven'
	}
	mavenCentral()
}

dependencies {
	minecraft("net.minecraftforge:forge:${minecraftVersion}-${forgeVersion}")
	compile("org.spongepowered:mixin:${mixinVersion}") {
		exclude module: 'launchwrapper'
		exclude module: 'guava'
		exclude module: 'gson'
		exclude module: 'commons-io'
	}
	annotationProcessor("org.spongepowered:mixin:${mixinProcessor}:processor") {
		exclude module: 'gson'
	}
	compile("com.github.ZeroMemes:Alpine:${alpineVersion}")
	compile("net.jodah:typetools:${typetoolsVersion}")
	compile("com.googlecode.json-simple:json-simple:${jsonVersion}")
	compile("club.minnced:java-discord-rpc:${discordVersion}")
	compile("com.lukflug:panelstudio:${psVersion}")
	compile("com.lukflug:panelstudio-mc12:${psmcVersion}")
}

processResources {
	inputs.property 'version', project.version
	inputs.property 'mcversion', project.minecraftVersion

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraftVersion
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}

	rename '(.+_at.cfg)', 'META-INF/$1'
}

mixin {
	defaultObfuscationEnv 'searge'
	add sourceSets.main, 'mixins.gamesense.refmap.json'
}

jar {
	from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
	manifest {
		attributes(
			'MixinConfigs': 'mixins.gamesense.json',
			'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
			'TweakOrder': 0,
			'FMLCorePluginContainsFMLMod': 'true',
			'FMLCorePlugin': 'com.gamesense.mixin.GameSenseMixinLoader',
			'ForceLoadAsMod': 'true',
			'FMLAT': 'gamesense_at.cfg'
		)
	}
} 
